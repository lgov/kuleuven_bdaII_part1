weibull_model{
    for (i in 1:N){
        y[i] ~ dweib(rho, lambda[i])
        # use the jth plate for the ith observation
        mu[i] <- betas[1] + betas[2]*group[i] + betas[3]*grubsize[i] + plate_unident[urepid[i]]
        lambda[i] <- exp(mu[i])
        # PPO
        log_ppo[i] <- log(rho*lambda[i]) + (rho - 1)*log(y[i]*lambda[i]) - pow(y[i]*lambda[i], rho)
        ppo[i] <- exp(log_ppo[i])
        # basis for computing CPO
        icpo[i]  <- 1/ppo[i]
    }

    # main effects
    for (k in 1:3){
        mu_beta[k] <- 0
    }
    tau_b <- .000001
    for (m in 1:3){
      for (n in 1:3){
        prec_beta[m,n] <- equals(m,n)*tau_b
      }
    }
    # Main way to improve convergence
    betas[1:3] ~ dmnorm(mu_beta[], prec_beta[,])
    # weibull param
    rho ~ dgamma(1, .0001)

    # Set up priors for each plate, i.e., the distribution of the random effects
    # Zero centered priors ("post sweeping")
    for (j in 1:J){
      plate_unident[j] ~ dnorm(mu_plate, tau_plate)
      # Monitor this set of effects
      plate[j] <- plate_unident[j] - avg_plate
    }
    # Intercept to monitor
    beta_star <- betas[1] + avg_plate
    # average of unindentified effects
    avg_plate <- mean(plate_unident[])
    
    tau_plate <- pow(sd_plate, -1)
    sd_plate ~ dunif(0,10)
    mu_plate ~ dnorm(0, .0001)

}
